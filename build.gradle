apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.5

group = 'com.bendb.gradle'
version = '1.6.1-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
  distributionUrl = 'http://services.gradle.org/distributions/gradle-2.1-all.zip'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.0.0'
    compile 'com.squareup.wire:wire-compiler:1.6.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
def stagingUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: snapshotUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Wire Protocol Buffer Gradle Plugin'
                packaging 'jar'
                description 'wire-gradle-plugin is a Gradle plugin for building protobuf classes with Square\'s Wire library'
                url 'http://github.com/square/wire/pull/177'
                inceptionYear '2014'

                scm {
                    url 'scm:git@github.com:benjamin-bader/wire-gradle-plugin.git'
                    connection 'scm:git@github.com:benjamin-bader/wire-gradle-plugin.git'
                    developerConnection 'scm:git@github.com:benjamin-bader/wire-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jpd236'
                        name 'Jeff Davidson'
                    }
                }
            }
        }
    }
}